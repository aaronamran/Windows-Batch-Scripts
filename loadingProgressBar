@echo off
setlocal enableextensions enabledelayedexpansion
title Loading Progress Bar
color 0a

Rem Get a carriage return character
set "CR=" & for /f %%a in ('copy /Z "%~f0" nul') do if not defined CR set "CR=%%a"
rem The progress bar
set "fill=[##########]"

cls

rem For each character in the fill
for /l %%a in (2 1 11) do (
    rem Calculate the right part of the bar
    set "spaces=!fill:~%%a!"

    rem Output the left and right parts of the bar and carriage return
    <nul set/p ".=Loading... !fill:~0,%%a!!spaces:#= !!CR!"

    rem Pause for a second
    ping -n 2 "" > nul
)

echo(
echo(Loading complete

rem Add a delay after printing "Done" message
timeout /t 2 > nul

rem End of script
exit /b
